#!/usr/bin/perl

use strict;
use warnings;
use feature 'say';

use Term::ReadLine ();
use File::Temp ();
use File::Spec ();

my $program_name = (File::Spec->splitpath($0))[2];

my $language = 'C';
if ($program_name eq 'c++repl' or $program_name eq 'cpprepl') {
    $language = 'C++';
}

my @includes = $language eq 'C' ? qw(<stdio.h>) : qw(<iostream>);

my (undef, $program) = File::Temp::tempfile("creplXXXXXX", TMPDIR => 1);
my $source_file = $language eq 'C' ? "$program.c" : "$program.cpp";
if ($^O eq 'cygwin') {
    $program .= '.exe';
}
my $use_math_lib = 0;

say "Type 'h' or '?' for help";

my $term = new Term::ReadLine($program_name);
$term->ornaments(0);
while (defined (my $line = $term->readline('{} '))) {
    if ($line =~ /^\s*(h|help|\?)\s*$/) {
        help();
    }
    elsif ($line =~ /^\s*q\s*$/) {
        exit;
    }
    elsif ($line =~ /^\s*include\s*$/) {
        show_includes();
    }
    elsif ($line =~ /^\s*include\b\s*(.*)$/) {
        add_include($1);
    }
    else {
        my @files = ();
        my $print_source = 0;
        if ($line =~ /^\s*p\b\s*(.*)$/) {
            $print_source = 1;
            $line = $1;
        }
        if ($line =~ /^\s*([df])\b\s*(.*)$/) {
            my($format, $expr) = ($1, $2);
            $line = qq{printf("%$format\\n", $expr)};
        }
        create_source($source_file, $line, $print_source);
        push @files, $source_file;
        my @gcc = $language eq 'C' ? qw(gcc -std=c11) : qw(g++ -std=c++11);
        my @lm = $use_math_lib ? qw(-lm) : ();
        my $result = system @gcc, '-pedantic-errors', $source_file, '-o', $program, @lm;
        if ($result == 0) {
            push @files, $program;
            system $program;
            # TODO: Show exit status
        }
        else {
            say "Compilation failed";
        }
        unlink @files if @files;
    }
}

say '';

########################################################################

# Returns a (possibly multi-line) string containing #include directives.
# The string is terminated by a newline.
sub includes {
    my $result = '';
    foreach my $include (@includes) {
       $result .=  "#include $include\n";
    }
    return $result;
}

# Returns the head of the generated C source file
sub head {
    my $main = $language eq 'C' ? 'int main(void)' : 'int main()';
    return includes() . "$main {\n";
}

# Returns the tail of the generated C source file
sub tail {
    return "    ;\n}\n";
}

sub show_includes {
    print includes();
}

sub add_include {
    my($include) = @_;
    if ($include !~ /^<.*>$/ and $include !~ /^".*"$/) {
        $include = "<$include>";
    }
    if ($include eq '<math.h>' or $include eq '<cmath>' or $include eq '<tgmath.h>') {
        $use_math_lib = 1;
    }
    push @includes, $include;
}

sub create_source {
    my($source_file, $line, $print_source) = @_;
    open my $SOURCE, '>', $source_file;
    print $SOURCE head(), "    $line\n", tail();
    if ($print_source) {
        print head(), "    $line\n", tail();
    }
    close $SOURCE;

    # say ">>> $source_file :";
    # system 'cat', $source_file;
    # say ">>> __END__";
}

sub help {
    print <<"EOF";
Enter a C statement
Other commands:
include        List current include list
include HEADER Add a header to the include list
p statement    Print the source file before compiling it
d expr         Print expr with "%d\\n"
f expr         Print expr with "%f\\n"
h, ?           Print this help message
q              Quit
EOF
}
